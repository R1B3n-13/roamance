plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id "com.diffplug.spotless" version "7.0.2"
    id "org.sonarqube" version "6.0.1.5171"
    id 'jacoco'
}

group = 'com.devs'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.6'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.postgresql:postgresql:42.7.5'
    implementation 'org.modelmapper:modelmapper:3.2.2'
    implementation 'io.jsonwebtoken:jjwt:0.12.6'
    implementation 'dev.langchain4j:langchain4j-spring-boot-starter:1.0.0-beta3'
    implementation 'dev.langchain4j:langchain4j-google-ai-gemini:1.0.0-beta3'
    implementation 'dev.langchain4j:langchain4j-reactor:1.0.0-beta3'
    implementation 'dev.langchain4j:langchain4j-nomic:1.0.0-beta3'
    implementation 'dev.langchain4j:langchain4j-pgvector:1.0.0-beta3'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'com.h2database:h2'
}

spotless.java {
    googleJavaFormat(googleJavaFormatVersion)
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

sonarqube {
    properties {
        property "sonar.projectKey", "Learnathon-By-Geeky-Solutions_devs"
        property "sonar.organization", "learnathon-by-geeky-solutions"
        property "sonar.host.url", System.getenv("SONAR_HOST_URL") ?: "https://sonarcloud.io"
        property "sonar.token", System.getenv("SONAR_TOKEN")
        property "sonar.sources", "src/main/java"
        property "sonar.tests", "src/test/java"
        property "sonar.java.binaries", "build/classes/java/main"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.coverage.inclusions", "**/com/devs/roamance/service/impl/**"
        property "sonar.coverage.exclusions", "**/com/devs/roamance/config/**, **/com/devs/roamance/controller/**, **/com/devs/roamance/service/aiimpl/**, **/com/devs/roamance/dto/**, **/com/devs/roamance/constant/**, **/com/devs/roamance/exception/**, **/com/devs/roamance/model/**, **/com/devs/roamance/pojo/**, **/com/devs/roamance/security/**, **/com/devs/roamance/util/**, **/com/devs/roamance/repository/**"
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

jacoco {
    toolVersion = "0.8.12"
}
